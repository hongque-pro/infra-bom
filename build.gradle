
buildscript {
    ext {
        publishing_plugin_version = '1.1.0'
    }
}

plugins {
    id "io.github.gradle-nexus.publish-plugin" version "$publishing_plugin_version" apply true
}


static Boolean isNotNullOrBlank(Object s) {
    return !(s == null || s.toString().trim().length() == 0)
}


static getPropertyOrCmdArgs(Project project, String propertyName, String cmdArgName) {
    def propertyValue = project.hasProperty(propertyName) ? project.property(propertyName).toString() : null
    def v = (System.properties.getProperty(cmdArgName) ?: propertyValue) ?: System.getenv(propertyName)
    return isNotNullOrBlank(v) ? v : null
}

def u = getPropertyOrCmdArgs(project, "PUB_USER", "u")
def p = getPropertyOrCmdArgs(project, "PUB_PWD", "p")
def s = getPropertyOrCmdArgs(project, "PUB_URL", "s")


subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'java-platform'

    group = 'com.labijie.bom'
    version = '2.2.0'


    javaPlatform {
        allowDependencies()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {

                artifactId = project.name + '-dependencies'
                from components.javaPlatform
                pom {
                    name = project.name
                    description = 'maven bom project'
                    url = 'https://github.com/hongque-pro/infra-bom'
//                    properties = [
//                            myProp: "value",
//                            "prop.with.dots": "anotherValue"
//                    ]
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'labijie infra team'
                            name = 'infra team'
                            email = 'tech@labijie.com'
                        }
                    }
                    scm {
                        url = 'https://github.com/hongque-pro/infra-bom'
                        connection = 'scm:git@github.com:hongque-pro/infra-bom.git'
                        developerConnection = 'git@github.com:hongque-pro/infra-bom.git'
                    }
                }
            }
        }



        repositories {
            // publish to private nexus
            // gradle -Du="username" -Dp='password' -Ds='http://your-nexus-address/repository/maven-releases/' publishMavenJavaPublicationToNexusRepository
            if (isNotNullOrBlank(u) && isNotNullOrBlank(p) && isNotNullOrBlank(s)) {
                maven {
                    name = "nexus"
                    url = s
                    credentials {
                        username = u
                        password = p
                    }
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = u
            password = p
        }
    }
}


